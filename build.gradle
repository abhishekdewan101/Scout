import com.abhishek101.gamescout.Libs
import com.abhishek101.gamescout.Versions

buildscript {
    repositories {
        google()
        mavenCentral()
        jcenter()
    }

    dependencies {
        classpath Libs.androidGradlePlugin
        classpath Libs.Kotlin.gradlePlugin
        classpath Libs.SqlDelight.gradlePlugin
        classpath "org.jetbrains.kotlin:kotlin-serialization:${Libs.Kotlin.version}"
        classpath Libs.buildKonfig
    }
}

plugins {
    id 'com.diffplug.spotless' version '5.10.0'
}

subprojects {
    repositories {
        google()
        mavenCentral()

        // Jetpack Compose SNAPSHOTs
        if (!Libs.AndroidX.Compose.snapshot.isEmpty()) {
            maven { url "https://androidx.dev/snapshots/builds/${Libs.AndroidX.Compose.snapshot}/artifacts/repository/" }
            maven { url "http://androidx.dev/storage/repository/androidx/constraintlayout" }
        }

        maven { url 'https://oss.sonatype.org/content/repositories/snapshots/' }
    }

    apply plugin: 'com.diffplug.spotless'
    spotless {
        kotlin {
            target '**/*.kt'
            targetExclude("$buildDir/**/*.kt")
            targetExclude('bin/**/*.kt')

            ktlint(Versions.ktlint)
        }
    }

    tasks.withType(org.jetbrains.kotlin.gradle.tasks.KotlinCompile).configureEach {
        kotlinOptions {
            // Treat all Kotlin warnings as errors
            allWarningsAsErrors = false

            freeCompilerArgs += '-Xopt-in=kotlin.RequiresOptIn'

            // Enable experimental coroutines APIs, including Flow
            freeCompilerArgs += '-Xopt-in=kotlinx.coroutines.ExperimentalCoroutinesApi'
            freeCompilerArgs += '-Xopt-in=kotlinx.coroutines.FlowPreview'
            freeCompilerArgs += '-Xopt-in=kotlin.Experimental'

            // Set JVM target to 1.8
            jvmTarget = "1.8"
        }
    }
}